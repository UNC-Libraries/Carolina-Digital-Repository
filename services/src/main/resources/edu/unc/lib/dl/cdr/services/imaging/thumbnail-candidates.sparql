PREFIX fedora-view: <info:fedora/fedora-system:def/view#> 
PREFIX fedora-model: <info:fedora/fedora-system:def/model#> 
PREFIX cdr-base: <http://cdr.unc.edu/definitions/1.0/base-model.xml#> 
SELECT ?pid ?srcDS ?srcMime ?thumbDS ?lastMod
FROM <%1$s>
WHERE 
{ 
 { # source image ds modified after thumb ds
   ?pid cdr-base:sourceData ?srcDS . 
   ?srcDS fedora-view:lastModifiedDate ?srcModified . 
   ?pid cdr-base:hasSourceMimeType ?srcMime .
   ?pid cdr-base:thumb ?thumbDS . 
   ?thumbDS fedora-view:lastModifiedDate ?thumbModified . 
   FILTER ( 
     regex( str(?srcMime), 'image/')
     && regex( str(?thumbDS), '%2$s') 
     && ?srcModified > ?thumbModified 
   ) 
 } UNION { # source image ds exists but thumb ds does not
   ?pid cdr-base:sourceData ?srcDS . 
   ?pid cdr-base:hasSourceMimeType ?srcMime .
   OPTIONAL {?pid cdr-base:thumb ?thumbDS .} 
   FILTER ( regex( str(?srcMime), 'image/') &&  !bound(?thumbDS) ) 
 } UNION { # source image ds exists but thumb ds is not a real ds
   ?pid cdr-base:sourceData ?srcDS . 
   ?pid cdr-base:hasSourceMimeType ?srcMime .
   OPTIONAL {?pid cdr-base:thumb ?thumbDS .
     ?thumbDS fedora-view:lastModifiedDate ?thumbModified . } 
   FILTER ( regex( str(?srcMime), 'image/') && !bound(?thumbDS) && !bound(?thumbModified) ) 
 } UNION  { # surrogate image ds modified after thumb ds
   ?pid cdr-base:hasSurrogate ?srg .
   ?srg cdr-base:sourceData ?srcDS . 
   ?srcDS fedora-view:lastModifiedDate ?srcModified . 
   ?srg cdr-base:hasSourceMimeType ?srcMime .
   ?pid cdr-base:thumb ?thumbDS . 
   ?thumbDS fedora-view:lastModifiedDate ?thumbModified . 
   FILTER ( 
     regex( str(?srcMime), 'image/')
     && regex( str(?thumbDS), '%2$s') 
     && ?srcModified > ?thumbModified 
   ) 
 } UNION { # surrogate image ds exists but thumb ds does not
   ?pid cdr-base:hasSurrogate ?srg .
   ?srg cdr-base:sourceData ?srcDS . 
   ?srg cdr-base:hasSourceMimeType ?srcMime .
   OPTIONAL {?pid cdr-base:thumb ?thumbDS .} 
   FILTER ( regex( str(?srcMime), 'image/') &&  !bound(?thumbDS) ) 
 } UNION { # surrogate image ds exists but thumb ds is not a real ds
   ?pid cdr-base:hasSurrogate ?srg .
   ?srg cdr-base:sourceData ?srcDS . 
   ?srg cdr-base:hasSourceMimeType ?srcMime .
   OPTIONAL {?pid cdr-base:thumb ?thumbDS .
     ?thumbDS fedora-view:lastModifiedDate ?thumbModified . } 
   FILTER ( regex( str(?srcMime), 'image/') && !bound(?thumbDS) && !bound(?thumbModified) ) 
 }
 ?pid fedora-view:lastModifiedDate ?lastMod
}
ORDER BY ASC(?lastMod)
LIMIT %3$s
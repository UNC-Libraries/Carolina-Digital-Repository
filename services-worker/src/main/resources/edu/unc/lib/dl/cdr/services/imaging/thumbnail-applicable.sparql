PREFIX fedora-view: <info:fedora/fedora-system:def/view#> 
PREFIX fedora-model: <info:fedora/fedora-system:def/model#> 
PREFIX cdr-base: <http://cdr.unc.edu/definitions/1.0/base-model.xml#> 
ASK 
FROM <%1$s>
WHERE 
{ # 1) image source data more recent than thumb data
  { ?pid cdr-base:sourceData ?srcDS . 
    ?srcDS fedora-view:lastModifiedDate ?srcModified . 
    ?pid cdr-base:hasSourceMimeType ?srcMime .
    ?pid <%3$s> ?thumbDS . 
    ?thumbDS fedora-view:lastModifiedDate ?thumbModified . 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && ?srcModified > ?thumbModified 
    ) 
  } UNION { # 2) image source data with no thumb predicate and no thumb data
    ?pid cdr-base:sourceData ?srcDS .
    ?pid cdr-base:hasSourceMimeType ?srcMime .
    OPTIONAL { ?pid <%3$s> ?thumbDS . 
      ?thumbDS fedora-view:lastModifiedDate ?thumbModified . } 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && !bound(?thumbDS) && !bound(?thumbModified) )
  } UNION { # 3) image source data with no thumb data
    ?pid cdr-base:sourceData ?srcDS .
    ?pid cdr-base:hasSourceMimeType ?srcMime .
    OPTIONAL { ?pid <%3$s> ?thumbDS . } 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && !bound(?thumbDS) )
  } UNION { # 1b) surrogate has image source data more recent than thumb data
    ?pid cdr-base:hasSurrogate ?srg .
    ?srg cdr-base:sourceData ?srcDS . 
    ?srcDS fedora-view:lastModifiedDate ?srcModified . 
    ?srg cdr-base:hasSourceMimeType ?srcMime .
    ?pid <%3$s> ?thumbDS . 
    ?thumbDS fedora-view:lastModifiedDate ?thumbModified . 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && ?srcModified > ?thumbModified 
    ) 
  } UNION { # 2b) surrogate has image source data with no thumb predicate and no thumb data
    ?pid cdr-base:hasSurrogate ?srg .
    ?srg cdr-base:sourceData ?srcDS .
    ?srg cdr-base:hasSourceMimeType ?srcMime .
    OPTIONAL { ?pid <%3$s> ?thumbDS . 
      ?thumbDS fedora-view:lastModifiedDate ?thumbModified . } 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && !bound(?thumbDS) && !bound(?thumbModified) )
  } UNION { # 3b) surrogate has image source data, pid has no thumb ds
    ?pid cdr-base:hasSurrogate ?srg .
    ?srg cdr-base:sourceData ?srcDS .
    ?srg cdr-base:hasSourceMimeType ?srcMime .
    OPTIONAL { ?pid <%3$s> ?thumbDS . } 
    FILTER (
      ?pid = <%2$s> 
      && regex( str(?srcMime), 'image/')
      && !bound(?thumbDS) )
  }
}